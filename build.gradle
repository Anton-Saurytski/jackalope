buildscript {
    repositories {
        jcenter()
        maven { name = 'Defaults Plugin Repo'; url = 'http://dl.bintray.com/jmoore/java-lib' }
        maven { name = 'Gradle Plugin Portal'; url = 'https://plugins.gradle.org/m2/' }
        mavenLocal()
    }

    dependencies {
        classpath 'com.mooregreatsoftware:gradle-defaults:3.0.0'
    }
}

plugins {
    id 'groovy'
    id 'com.jfrog.bintray' version '1.7.1'
}

apply plugin: 'com.mooregreatsoftware.defaults'

defaults {
    // make sure configuration is done for enforcing licenses, BinTray, etc.
    openSource = true

    compatibilityVersion = 1.8

    id = 'twcable'

    orgName = 'Time Warner Cable'
    orgUrl = 'http://www.timewarnercable.com'

    developers = [
        [id: 'jmoore', name: 'Jim Moore', email: 'moore.jim@gmail.com']
    ]

    copyrightYears = '2014-2016'

    vcsWriteUrl = "git@github.com:jdigger/gradle-defaults.git"

    lombok.enabled = true
    checkerFramework.enabled = true
}

bintray {
    configurations = ['archives']

    pkg {
        userOrg = 'twcable'
        repo = 'aem'
        name = project.name

        desc = project.description

        websiteUrl = 'https://github.com/TWCable/jackalope'
        issueTrackerUrl = 'https://github.com/TWCable/jackalope/issues'
        vcsUrl = 'https://github.com/TWCable/jackalope.git'
        licenses = ['Apache-2.0']
        labels = ['groovy', 'cq', 'aem', 'jcr', 'sling']
        attributes = ['plat': ['aem', 'cq', 'jcr', 'sling']]

        publicDownloadNumbers = true
    }
}

repositories {
    jcenter()

    // Adobe-specific artifacts
    maven {
        url "http://repo.adobe.com/nexus/content/groups/public"
    }
}


dependencies {

    // AEM's new "uber-jar"
    compile "com.adobe.aem:aem-api:6.0.0.1", {
        exclude group: "org.slf4j", module: "slf4j-simple"
        exclude group: "com.adobe.cq.testing", module: "cq-integration-testing"
    }

    compile "javax.jcr:jcr:2.0"
    compile "com.google.guava:guava:15.0"
    compile "org.apache.sling:org.apache.sling.jcr.resource:2.3.8"
    compile "org.slf4j:slf4j-api:1.6.6"
    compile "javax.servlet:servlet-api:2.5"
    compile "commons-io:commons-io:2.4"

    // @Nonnull @Nullable etc annotations
    compile "com.google.code.findbugs:jsr305:2.0.1"

    runtime "ch.qos.logback:logback-classic:1.0.4"

    testCompile "org.codehaus.groovy:groovy:2.3.6"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0", {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

wrapper {
    gradleVersion = '2.14.1'
}

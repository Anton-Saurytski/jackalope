apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

//
// Make sure the needed properties exist if the user is trying to upload to Bintray
//
gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(bintrayUpload)) {
        if (!project.hasProperty('bintray.user') || !project.hasProperty('bintray.key')) {
            throw new IllegalArgumentException((String)"Please define 'bintray.user' and " +
                "'bintray.key' properties. (Such as in ~/.gradle/gradle.properties)")
        }
    }
}

//noinspection GroovyAssignabilityCheck
bintray {
    user = project.properties['bintray.user']
    key = project.properties['bintray.key']
    configurations = ['archives']

    publish = version.status == 'release'

    pkg {
        userOrg = 'twcable'
        repo = 'aem'
        name = project.name

        desc = project.description

        websiteUrl = 'https://github.com/TWCable/jackalope'
        issueTrackerUrl = 'https://github.com/TWCable/jackalope/issues'
        vcsUrl = 'https://github.com/TWCable/jackalope.git'
        licenses = ['Apache-2.0']
        labels = ['groovy', 'cq', 'aem', 'jcr', 'sling']
        attributes = ['plat': ['aem', 'cq', 'jcr', 'sling']]

        publicDownloadNumbers = true

        //noinspection GroovyAssignabilityCheck
        version {
            desc = 'First public release'
            //noinspection GrReassignedInClosureLocalVar
            name = project.version.bintrayVersion
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}
